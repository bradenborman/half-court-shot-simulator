plugins {
	id 'org.springframework.boot' version '2.3.4.RELEASE'
	id 'io.spring.dependency-management' version '1.0.10.RELEASE'
	id 'java'
	id 'war'
	id 'groovy'
}

group = 'borman'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '1.8'

repositories {
	mavenCentral()
}

dependencies {

	implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	providedRuntime 'org.springframework.boot:spring-boot-starter-tomcat'

	//Testing

	//Spock
	implementation 'org.codehaus.groovy:groovy-all:2.4.11'
	testCompile "org.spockframework:spock-core:1.1-groovy-2.4-rc-2"

	testImplementation('org.springframework.boot:spring-boot-starter-test') {
		exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
	}

}

test {
	exclude '**/*integration/**'
}


task copyClient(dependsOn: ":hcs-client:clientBuild") {
	doLast {
		copy {
			from new File(project(":hcs-client").getProjectDir(), "build")
			into "${buildDir}/resources/main/static"
		}
		copy {
			from new File(project(":hcs-client").getProjectDir(), "build/index.html")
			into "${buildDir}/resources/main/templates"
		}
		copy {
			from new File(project(":hcs-client").getProjectDir(), "build/error.html")
			into "${buildDir}/resources/templates"
		}
	}
}

task finalizeBuild(type: Copy, dependsOn: build) {
	from "${buildDir}/libs/server-${version}.war"
	into "${buildDir}/libs"
	rename "server-${version}.war", "half-court-shot-simulator.war"
}

bootWar.dependsOn(copyClient)
clean.dependsOn(":hcs-client:clientClean")
build.finalizedBy(finalizeBuild)

task stage(dependsOn: ['build', 'clean'])
build.mustRunAfter clean